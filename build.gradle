plugins {
    id 'biz.aQute.bnd.builder' version '3.3.0'
}

sourceCompatibility = '1.8'
version = '5.3.8.2'

repositories {
    maven { url "https://repo.dotcms.com/artifactory/libs-release" }
}
configurations {
    osgiLibs
}
dependencies {


    compile  ('com.dotcms:dotcms:5.3.8') { transitive = true }
    compile  ('org.opensaml:opensaml-core:3.3.1') { transitive = false }
    compile  ('org.opensaml:opensaml-saml-api:3.3.1') { transitive = false }
    compile  ('org.opensaml:opensaml-security-api:3.3.1') { transitive = false }
    compile  ('org.opensaml:opensaml-xmlsec-impl:3.3.1') { transitive = false }
    compile  ('org.opensaml:opensaml-xmlsec-api:3.3.1') { transitive = false }
    compile  ('org.opensaml:opensaml-security-impl:3.3.1') { transitive = false }
    compile  ('org.opensaml:opensaml-messaging-impl:3.3.1') { transitive = false }
    compile  ('org.opensaml:opensaml-messaging-api:3.3.1') { transitive = false }
    compile  ('org.opensaml:opensaml-storage-impl:3.3.1') { transitive = false }
    compile  ('org.opensaml:opensaml-storage-api:3.3.1') { transitive = false }
    compile  ('org.opensaml:opensaml-xacml-api:3.3.1') { transitive = false }
    compile  ('org.opensaml:opensaml-xacml-saml-api:3.3.1') { transitive = false }
    compile  ('org.opensaml:opensaml-xacml-saml-impl:3.3.1') { transitive = false }
    compile  ('org.opensaml:opensaml-saml-impl:3.3.1') { transitive = false }
    compile  ('org.opensaml:opensaml-soap-api:3.3.1') { transitive = false }
    compile  ('org.opensaml:opensaml-soap-impl:3.3.1') { transitive = false }
    compile  ('org.apache.servicemix.bundles:org.apache.servicemix.bundles.opensaml:3.3.1_1')
    compile  ('org.glassfish.jersey.media:jersey-media-jaxb:2.22.1') { transitive = false }
    
    // everything except dotCMS

    osgiLibs  ('org.opensaml:opensaml-core:3.3.1') { transitive = false }
    osgiLibs  ('org.opensaml:opensaml-saml-api:3.3.1') { transitive = false }
    osgiLibs  ('org.opensaml:opensaml-security-api:3.3.1') { transitive = false }
    osgiLibs  ('org.opensaml:opensaml-xmlsec-impl:3.3.1') { transitive = false }
    osgiLibs  ('org.opensaml:opensaml-xmlsec-api:3.3.1') { transitive = false }
    osgiLibs  ('org.opensaml:opensaml-security-impl:3.3.1') { transitive = false }
    osgiLibs  ('org.opensaml:opensaml-messaging-impl:3.3.1') { transitive = false }
    osgiLibs  ('org.opensaml:opensaml-messaging-api:3.3.1') { transitive = false }
    osgiLibs  ('org.opensaml:opensaml-storage-impl:3.3.1') { transitive = false }
    osgiLibs  ('org.opensaml:opensaml-storage-api:3.3.1') { transitive = false }
    osgiLibs  ('org.opensaml:opensaml-xacml-api:3.3.1') { transitive = false }
    osgiLibs  ('org.opensaml:opensaml-xacml-saml-api:3.3.1') { transitive = false }
    osgiLibs  ('org.opensaml:opensaml-xacml-saml-impl:3.3.1') { transitive = false }
    osgiLibs ('org.glassfish.jersey.media:jersey-media-jaxb:2.22.1') { transitive = false }
    osgiLibs  ('org.opensaml:opensaml-saml-impl:3.3.1') { transitive = false }
    osgiLibs  ('org.opensaml:opensaml-soap-api:3.3.1') { transitive = false }
    osgiLibs  ('org.opensaml:opensaml-soap-impl:3.3.1') { transitive = false }
    osgiLibs  ('org.apache.servicemix.bundles:org.apache.servicemix.bundles.opensaml:3.3.1_1'){
        exclude(group: 'commons-logging')
        exclude(group: 'org.slf4j')
    }
    
    
    testCompile 'junit:junit:4.12'
    
}

import java.util.jar.*
jar.baseName = 'com.dotcms.samlbundle'
/////////////////////////
//Plugin jar
/////////////////////////
jar {
    manifest {
        attributes (
                'Bundle-Vendor': 'dotCMS',
                'Bundle-Description': 'dotCMS - SAML Provider',
                'Bundle-DocURL': 'https://dotcms.com/',
                'Bundle-Activator': 'com.dotcms.saml.osgi.Activator',
                'Bundle-ClassPath' : "${classPathLibraries()}",
                'DynamicImport-Package': '*',
                'Import-Package': '''
                    javax.servlet;version=0,
                    javax.servlet.jsp.*;resolution:=optional,
                    javax.servlet.annotation;version=0,
                    javax.servlet.http;version=0,
                    javax.naming;version=0,
                    !org.apache.velocity.*,
                    !com.google.common.*,
                    !org.opensaml.*,
                    *;version=0.1
                    
                '''
        )
    }
}

task copyToLib(type: Copy) {
    into "src/main/resources/libs"
    from configurations.osgiLibs
}

compileJava.dependsOn copyToLib
jar.finalizedBy 'fragmentJar'

/**
 * Searches for jars inside the src/main/resources/libs folder, the returned list is used for the
 * Bundle-ClassPath attribute.
 * @return String with the list of jars inside the src/main/resources/libs folder or empty if the
 * folder does not exist or it is empty.
 */
def classPathLibraries() {

    def bundleClassPath = "";
    fileTree("src/main/resources/libs").filter { it.isFile() }.each { bundleClassPath += "libs/" + it.name + "," }

    if (bundleClassPath != "") {
        bundleClassPath = '.,' + bundleClassPath
    }

    return bundleClassPath
}

/////////////////////////
//Fragment jar
/////////////////////////

ext {
    bundleName = "OSGI 3rd Party library fragment"
    bundleDescription = "dotCMS - OSGI 3rd Party library fragment"
    fragmentHost = "system.bundle; extension:=framework"
    bundleSymbolicName = "" //Auto generated based on the plugin jar
    bundleVersion = "" //Auto generated based on the plugin jar
    importPackage = "" //Auto generated based on the plugin jar
    bundleManifestVersion = "" //Auto generated based on the plugin jar
    bundleDocURL = "" //Auto generated based on the plugin jar
    bundleVendor = "" //Auto generated based on the plugin jar
}

/**
 * The import generates versions like this: version="[1.8,2)"
 * That format does not work for the export, so we need to replace it
 * to: version=0
 */
ext.fixVersionNumber = {importValue ->
    return importValue.replaceAll("\"\\[[0-9.,]+\\)\"", "0")
}

/**
 * Reads the Manifest file of the just created plugin jar in order to get the required info
 * to automatically create the fragment jar.
 */
task readManifesttAttributes {
    doFirst {
        File file = configurations.baseline.singleFile
        JarFile jar = new JarFile(file)
        Attributes manifest = jar.getManifest().getMainAttributes()
        bundleSymbolicName = "${manifest.getValue('Bundle-SymbolicName')}"
        bundleVersion = "${manifest.getValue('Bundle-Version')}"
        importPackage = "${manifest.getValue('Import-Package')}"
        bundleManifestVersion = "${manifest.getValue('Bundle-ManifestVersion')}"
        bundleDocURL = "${manifest.getValue('Bundle-DocURL')}"
        bundleVendor = "${manifest.getValue('Bundle-Vendor')}"
    }
}

task fragmentJar(type: Jar) {

    doFirst {
        //Setting the fragment jar name
        baseName = jar.baseName
        archiveName = "${baseName}.fragment-${version}.jar"
        importPackage = fixVersionNumber(importPackage)

        manifest {
            attributes (
                    'Bundle-Name': "${bundleName}",
                    'Bundle-Description': "${bundleDescription}",
                    'Bundle-Vendor': "${bundleVendor}",
                    'Bundle-Version': "${version}",
                    'Bundle-SymbolicName': "${baseName}.fragment",
                    'Bundle-ManifestVersion': "${bundleManifestVersion}",
                    'Bundle-DocURL': "${bundleDocURL}",
                    'Fragment-Host': "${fragmentHost}",
                    'Export-Package': "${importPackage}"
            )
        }
    }
}
fragmentJar.dependsOn 'readManifesttAttributes'

task wrapper(type: Wrapper) {
    gradleVersion = '4.2'
}


